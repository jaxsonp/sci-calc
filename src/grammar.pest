target = _{ SOI ~ (assignment | add_expr) ~ EOI }
assignment =  {
	IDENT ~ EQUALS ~ add_expr
}

exp_expr =  {
	atom ~ CARET ~ exp_expr
	| atom
}
mult_expr =  {
	exp_expr ~ ((MULTIPLY | DIVIDE) ~ mult_expr)*
}
add_expr =  {
	mult_expr ~ ((PLUS | MINUS) ~ add_expr)*
}
atom = _{
	(L_PAREN ~ add_expr ~ R_PAREN)
	| NUM
	| function
	| IDENT
}

function = { IDENT ~ L_PAREN ~ arg_list ~ R_PAREN }
arg_list = {
	add_expr ~ COMMA ~ arg_list
	| add_expr
}

L_PAREN = _{ "(" }
R_PAREN = _{ ")" }
PLUS = { "+" }
MINUS = { "-" }
MULTIPLY = { "*" }
DIVIDE = { "/" }
CARET = { "^" }
EQUALS = { "=" }
COMMA = _{ "," }

NUM   = @{
	MINUS? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+
	| MINUS? ~ ASCII_DIGIT+
}
IDENT = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

WHITESPACE = _{ " " }
